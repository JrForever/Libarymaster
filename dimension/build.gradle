apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "V1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "company", "channel"
    }

    signingConfigs {
        release {
            storeFile file(STORE_FILE)
            storePassword STORE_PASSWORD
            keyAlias KEY_ALIAS
            keyPassword STORE_PASSWORD
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    productFlavors {
        qianlong {
            dimension "company"
            signingConfig signingConfigs.release
            applicationId "com.qianlong"
            buildConfigField "String","FLAVOR_NAME", "\"qianlong\""
            manifestPlaceholders = [appname : "qianlong"]
        }

        dongfang {
            dimension "company"
            signingConfig signingConfigs.release
            applicationId "com.dongfang"
            buildConfigField "String","FLAVOR_NAME", "\"dongfang\""
            manifestPlaceholders = [appname : "dongfang"]
        }

        channelA {
            dimension "channel"
            applicationIdSuffix ".channelA"
            buildConfigField "String","Channel_TYPE", "\"channelA\""
        }

        channelB {
            dimension "channel"
            applicationIdSuffix ".channelB"
            buildConfigField "String","Channel_TYPE", "\"channelB\""
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.applicationVariants.all { variant ->
        def date = new Date().format("yyyyMMdd")
        variant.getPackageApplication().outputDirectory = new File("E:\\apks","dimession")
        def vName = "${variant.versionName}(${date})"
        variant.outputs.all {
            outputFileName = "${variant.productFlavors[0].name}_${vName}_${variant.productFlavors[1].name}.apk"
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
